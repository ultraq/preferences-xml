/* 
 * Copyright 2007, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'groovy'
	id 'codenarc'
	id 'maven'
	id 'net.saliman.cobertura' version '2.5.1'
	id 'com.github.kt3k.coveralls' version '2.8.1'
}
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/2.1.0/gradle-support.gradle'
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/2.1.0/maven-support.gradle'
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/2.1.0/bundle.gradle'
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/2.1.0/jaxb.gradle'

//project.name      = 'preferences-xml'
project.description = 'An implementation of the Java Preferences API (java.util.prefs) to store preferences in an XML file, in a subdirectory of a Java program\'s working directory'
project.archivesBaseName = 'preferences-xml'
project.sourceCompatibility = '1.8'

project.group    = 'nz.net.ultraq.preferences'
project.artifact = 'preferences-xml'
project.version  = '2.0.0'
project.year     = '2007'

sourceSets {
	main {
		resources {
			srcDir 'schema'
		}
	}
}

repositories {
	flatDir(dirs: 'libraries')
}

dependencies {
	xjc(
		':xew:1.3.132:jdk6_jaxb2.2'
	)
	compile(
		'nz.net.ultraq.jaxb:jaxb-utilities:2.0.0',
		'org.codehaus.groovy:groovy:2.4.12'
	)
	testCompile(
		'junit:junit:4.12',
		'org.mockito:mockito-core:2.12.0'
	)
	testRuntime(
		'org.slf4j:slf4j-api:1.7.10'
	)
}

xjc {
	classes = [
		'**/ObjectFactory.java',
		'**/package-info.java',
		'**/XmlEntry.java',
		'**/XmlNode.java',
		'**/XmlRoot.java'
	]
	args = [
		'-Xxew',
		'-instantiate lazy',
		'-delete',
		'-Xinject-code'
	]
}

cobertura {
	coverageExcludes = [
	  '.*nz.net.ultraq.preferences.xml.ObjectFactory.*',
		'.*nz.net.ultraq.preferences.xml.XmlEntry.*',
		'.*nz.net.ultraq.preferences.xml.XmlNode.*',
		'.*nz.net.ultraq.preferences.xml.XmlRoot.*',
		'.*nz.net.ultraq.preferences.xml.package-info.*'
	]
}

// Disable doclint as it errors on many of the generated files
if (JavaVersion.current().java8Compatible) {
	javadoc {
		options {
			addStringOption('Xdoclint:none', '-quiet')
		}
	}
}
